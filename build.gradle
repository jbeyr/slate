
buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            url 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath "org.projectlombok:lombok:1.18.22"
    }
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'

group = "me.jbeyr"
archivesBaseName = "slate"

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}


minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += '-Dfml.coreMods.load=slate.mixins.MixinLoader'
}

mixin {
    defaultObfuscationEnv searge
    sourceSets {
        main {
            ext.refMap = 'mixins.slate.refmap.json'
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spongepowered.org/maven'
    }
}

configurations {
    embed
    compile.extendsFrom embed
}


dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.14.0'
    embed('org.spongepowered:mixin:0.6-SNAPSHOT') {
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'jarjar'
        exclude module: 'commons-codec'
        exclude module: 'commons-io'
        exclude module: 'launchwrapper'
        exclude module: 'asm-commons'
        exclude module: 'slf4j-api'
    }
    implementation 'org.projectlombok:lombok:1.18.34'
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation files('libraries/fastutil-core-8.5.14.jar')
    implementation files('libraries/json-20220924.jar')

    embed files('libraries/fastutil-core-8.5.14.jar')
    embed files('libraries/json-20220924.jar')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from(configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude 'dummyThing',
                'LICENSE.txt',
                'LICENSE',
                'AUTHORS',
                'README.md',
                'META-INF/MUMFREY.RSA',
                'META-INF/maven/**',
                'org/**/*.html',
                'release-timestamp.txt',
                'changelog.txt'
    }
    manifest {
        attributes(
                'MixinConfigs': 'mixins.slate.json',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0,
                "FMLCorePlugin": "slate.mixins.MixinLoader",
                "ForceLoadAsMod": true,
                'FMLCorePluginContainsFMLMod': true,
                "ModSide": "CLIENT"
        )
    }
}

task copyDep(type: Copy){
    from configurations.runtimeClasspath
    into 'dependencies'
}


reobf {
    jar {
        mappingType = "SEARGE"
    }
}
